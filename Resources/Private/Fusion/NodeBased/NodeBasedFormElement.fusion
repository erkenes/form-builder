prototype(Neos.Form.Builder:NodeBasedFormElement) < prototype(Neos.Fusion:Renderer) {
    element {
        @context.element = ${this}
        identifier = ${elementNode.properties.identifier || pageNode.aggregateId.value}
        label = ${elementNode.properties.label}
        required = ${elementNode.properties.required}
        defaultValue = ${elementNode.properties.defaultValue}
        properties.@process.addNodeProperties = ${Neos.Form.Builder.Node.mergeProperties(value, elementNode)}
        validators = Neos.Form.Builder:NodeBasedValidatorCollection {
            items = ${q(elementNode).children('validators').children()}
        }
        renderingOptions._node = ${elementNode}
        renderingOptions._fusionPath = ${element.path}

        properties.options.@process.overrideFromNode = Neos.Form.Builder:SelectOptionCollection {
            items = ${q(elementNode).children('options').children()}
            valuePropertyPath = 'properties.value'
            labelPropertyPath = 'properties.label'
            @if.isSelectFormElement = ${q(elementNode).is('[instanceof Neos.Form.Builder:SelectionMixin]')}
        }

        elements.@process.overrideFromNode = Neos.Form.Builder:NodeBasedElementCollection {
            items = ${q(elementNode).children('elements').children()}
            @if.isSectionFormElement = ${q(elementNode).is('[instanceof Neos.Form.Builder:SectionMixin]')}
        }
    }
}

prototype(Neos.Form.Builder:NodeBasedElementCollection) < prototype(Neos.Fusion:Loop) {
    itemName = 'elementNode'
    itemRenderer = Neos.Fusion:Case {
        formElementTypeFromNodeType {
            condition = ${!Neos.Node.nodeType(elementNode).options.form.formElementType}
            renderer = Neos.Form.Builder:NodeBasedFormElement {
                type = ${elementNode.nodeTypeName.value + '.Definition'}
            }
        }
        default {
            condition = ${true}
            renderer = Neos.Form.Builder:NodeBasedFormElement {
                type = 'Neos.Form.Builder:FormElement.Definition'
                element.formElementType = ${Neos.Node.nodeType(elementNode).options.form.formElementType}
            }
        }
    }
}
